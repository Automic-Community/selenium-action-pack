*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        run_test_suite
Default Tags
Test Template     Run Test Suite Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ServerPath                TestSuiteFile                TestResultFile      AUTUrl        Browser       AdvancedOptions
Run test suite with valid inputs
                      [Tags]    main_scenarios
                      OK        ${Server_Path}            ${Suite_File}                ${Result_File}      ${Aut_Url}    ${Browser}    ${Advanced_Options}

Run test suite without advanced options
                      [Tags]    main_scenarios
                      OK        ${Server_Path}            ${Suite_File}                ${Result_File}      ${Aut_Url}    ${Browser}    ${EMPTY}

Run test suite with result file as optional field
                      [Tags]    main_scenarios
                      OK        ${Server_Path}            ${Suite_File}                ${EMPTY}            ${Aut_Url}    ${Browser}    ${Advanced_Options}

Run test suite with invalid server path
                      [Tags]    main_scenarios
                      NOK       Invalid                   ${Suite_File}                ${Result_File}      ${Aut_Url}    ${Browser}    ${EMPTY}

Run test suite with invalid server file
                      [Tags]    main_scenarios
                      NOK       ${Invalid_Server_File}    ${Suite_File}                ${Result_File}      ${Aut_Url}    ${Browser}    ${EMPTY}

Run test suite with invalid test suite file path
                      [Tags]    main_scenarios
                      NOK       ${Server_Path}            Invalid                      ${Result_File}      ${Aut_Url}    ${Browser}    ${EMPTY}

Run test suite with corrupted test suite file
                      [Tags]    main_scenarios
                      NOK       ${Server_Path}            ${Corrupted_Suite_File}      ${Result_File}      ${Aut_Url}    ${Browser}    -timeout 30 -port 5577

Run test suite with unsupported test suite file name
                      [Tags]    main_scenarios
                      NOK       ${Server_Path}            ${Unsupported_Suite_File}    ${Result_File}      ${Aut_Url}    ${Browser}    ${EMPTY}

Run test suite with non-existing directory to result file
                      [Tags]    main_scenarios
                      NOK       ${Server_Path}            ${Suite_File}                ABC:\result.html    ${Aut_Url}    ${Browser}    ${EMPTY}

Run test suite with invalid aut url
                      [Tags]    main_scenarios
                      NOK       ${Server_Path}            ${Suite_File}                ${Result_File}      Invalid       ${Browser}    ${EMPTY}

Run test suite with invalid browser name
                      [Tags]    main_scenarios
                      NOK       ${Server_Path}            ${Suite_File}                ${Result_File}      ${Aut_Url}    Invalid       ${EMPTY}

Assert server path as mandatory field
                      [Tags]    main_scenarios
                      NOK       ${EMPTY}                  ${Suite_File}                ${Result_File}      ${Aut_Url}    ${Browser}    ${Advanced_Options}

Assert suite file as mandatory field
                      [Tags]    main_scenarios
                      NOK       ${Server_Path}            ${EMPTY}                     ${Result_File}      ${Aut_Url}    ${Browser}    ${Advanced_Options}

Assert aut url as mandatory field
                      [Tags]    main_scenarios
                      NOK       ${EMPTY}                  ${Suite_File}                ${Result_File}      ${EMPTY}      ${Browser}    ${Advanced_Options}

Assert browser as mandatory field
                      [Tags]    main_scenarios
                      NOK       ${EMPTY}                  ${Suite_File}                ${Result_File}      ${Aut_Url}    ${EMPTY}      ${Advanced_Options}

*** Keywords ***
Startup
    Log    *******Start Test "RUN TEST SUITE"*******
    Log    *******Connect to AE*******
    Connect AE

Run Test Suite Template
    [Arguments]    ${OK_NOK}    ${ServerPath}    ${TestSuiteFile}    ${TestResultFile}    ${AUTUrl}    ${Browser_Name}
    ...    ${AdvancedOptions}
    [Documentation]    This test is used to run a test suite on the specified browser.
    ...    -Precondition: To run this test suite selenium server path should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${ServerPath}: This field specifies the path and file name to the Selenium standalone server.
    ...    -${TestSuiteFile}: This field specifies Name of the suite file with full path.
    ...    -${TestResultFile}: This field specifies Name of the result file with full path. If not specified then default temporary path at agent will be used.
    ...    -${AUTUrl}: This field specifies type of the Url to the Application Under Test.
    ...    -${Browser_Name}: This field specifies Name of the browser in which you want to run test suite. It is case-sensitive.
    ...    -${AdvancedOptions}: This field specifies the the advanced options except for -htmlSuite.
    Action Create    ${_RUN_TEST_SUITE_ACTION}
    Action Set    &UC4RB_SLM_SERVER_FILE#    ${ServerPath}
    Action Set    &UC4RB_SLM_SUITE_FILE#    ${TestSuiteFile}
    Action Set    &UC4RB_SLM_RESULT_FILE#    ${TestResultFile}
    Action Set    &UC4RB_SLM_AUT_URL#    ${AUTUrl}
    Action Set    &UC4RB_SLM_BROWSER#    ${Browser_Name}
    Action Set    &UC4RB_SLM_OPTIONS#    ${AdvancedOptions}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_SLM_RESULT_FILE_PATH#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_SLM_TOTAL_TEST_CASES#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_SLM_PASSED_CASES#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_SLM_FAILED_CASES#

Teardown
    Log    *******End Test "RUN TEST SUITE"*******
